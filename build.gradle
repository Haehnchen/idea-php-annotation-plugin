import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id "org.jetbrains.intellij" version "0.4.11"
    id 'com.palantir.git-version' version "0.11.0"
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'antlr'
sourceCompatibility = 1.8
targetCompatibility = 1.8

def requiredPlugins = [
    "com.jetbrains.php:${phpPluginVersion}",
    "de.espend.idea.php.toolbox:${toolboxPluginVersion}",
    "com.jetbrains.twig:${twigPluginVersion}",
    'CSS',
    'java',
    'java-i18n',
    'properties'
]

if(ideaVersion.startsWith("IU-2019.1")) {
    requiredPlugins.removeAll { (it.toLowerCase() == 'java') }
}

intellij {
    version ideaVersion
    updateSinceUntilBuild false
    plugins = requiredPlugins
    pluginName 'PHP Annotations'


    // Can't instantiate configurable for PHP Toolbox
    // at de.espend.idea.php.toolbox.ui.application.ToolboxApplicationForm.<init>(ToolboxApplicationForm.java:26)
    tasks {
        "buildSearchableOptions" {
            enabled = false
        }
    }
}

patchPluginXml {
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
    sinceBuild '201'
}

publishPlugin {
    token System.getenv('IJ_TOKEN')
}

group 'de.espend.idea.php.annotation'

def details = versionDetails()
if (details.isCleanTag) {
    version = "${details.lastTag}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

wrapper {
    gradleVersion '5.5.1'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.8"
}

